AC_PREREQ(2.53)

AC_INIT([gnome-themes-clearlooks], [2.32.0],
        [http://bugzilla.gnome.org/enter_bug.cgi?product=gnome-themes])

AM_INIT_AUTOMAKE([1.9 tar-ustar])

# Enable silent build rules by default, requires at least
# Automake-1.11. Disable by either passing --disable-silent-rules to
# configure or passing V=1 to make
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

IT_PROG_INTLTOOL([0.35.0])
PKG_PROG_PKG_CONFIG([0.19])

GETTEXT_PACKAGE="${PACKAGE}"
AC_SUBST(GETTEXT_PACKAGE)

AM_GLIB_GNU_GETTEXT

# AM_GLIB_GNU_GETTEXT above subst. $DATADIRNAME
# this is the directory where the *.{mo,gmo} files are installed
localedir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(localedir)

# Workaround to make aclocal get the right flags
AC_SUBST(ACLOCAL_AMFLAGS, "\${ACLOCAL_FLAGS}")

# Check GTK+ theme engines
PKG_CHECK_MODULES(THEME_ENGINE,
 [gtk+-2.0 >= 2.0.0 dnl
  gdk-pixbuf-2.0 >= 2.0.0 dnl
  gtk-engines-clearlooks-2 >= 2.15.3 ])

GTK_VERSION=`$PKG_CONFIG --variable=gtk_binary_version gtk+-2.0`
AC_SUBST(GTK_VERSION)

# Check if it's a development (odd minor) version
AC_MSG_CHECKING([development series build])
MINOR_VERSION="`echo $PACKAGE_VERSION|cut -d. -f2`"
if expr $MINOR_VERSION % 2 > /dev/null != "0"; then
   # Development version, disable mapping
   # and enable placeholder by default
   icon_mapping_policy=no
   placeholders_policy=yes
   enable_test_themes=yes
   AC_MSG_RESULT([yes])
else
  # Stable version, enable mapping
  # and disable placeholders by default
  icon_mapping_policy=yes
  placeholders_policy=no
  AC_MSG_RESULT([no])
fi

AC_CONFIG_FILES([
Makefile
gnome-themes.spec
desktop-themes/Makefile
desktop-themes/Clearlooks/Makefile
metacity-themes/Makefile
po/Makefile.in
])

AC_OUTPUT

dnl ==========================================================================
echo "
gnome-themes-$VERSION:

        Installation prefix:	${prefix}

	Enable all themes:           ${enable_all_themes}

"
